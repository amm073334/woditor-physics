const int SCREEN_WIDTH = 1280;
const int SCREEN_HEIGHT = 720;

const int SCREEN_CENTER_X = SCREEN_WIDTH / 2;
const int SCREEN_CENTER_Y = SCREEN_HEIGHT / 2;

const int H_FOV = 900;
const int TAN_HFOV_2 = 1000;
const int TAN_VFOV_2 = TAN_HFOV_2 * SCREEN_HEIGHT / SCREEN_WIDTH;

const int MAX_QUADS = 100;

cdb Properties {
    int indexbuf_len;
    int vertexbuf_len;
    int[12] look_at;
    int[3] cam_pos;
}

cdb VertexBuffer {
    int[3] world_space;
    int[3] camera_space;
    int screen_x;
    int screen_y;
}

cdb IndexBuffer {
    int z;
    int is_line;
    int ul;
    int ur;
    int dl;
    int dr;
}

cdb Cubes {
    int[3] v0;
    int[3] v1;
    int[3] v2;
    int[3] v3;
    int[3] v4;
    int[3] v5;
    int[3] v6;
    int[3] v7;
}

inline void render_buffer_vert(int i, int[3] v) {
    vec3_copy(VertexBuffer[i].world_space, v);
    mat3x4_transform(VertexBuffer[i].camera_space, Properties[0].look_at, v);

    if (VertexBuffer[i].camera_space[2] <= 0) {
        return;
    }

    VertexBuffer[i].screen_x = 
        VertexBuffer[i].camera_space[0] * SCREEN_CENTER_X / 
        (VertexBuffer[i].camera_space[2] * TAN_HFOV_2 / 1000) + SCREEN_CENTER_X;
    VertexBuffer[i].screen_y = 
        -VertexBuffer[i].camera_space[1] * SCREEN_CENTER_Y / 
        (VertexBuffer[i].camera_space[2] * TAN_VFOV_2 / 1000) + SCREEN_CENTER_Y;
}

inline void render_draw() {
    cmd [150](16777218,0,0,0,0,MAX_QUADS)();

    int i = 0;
    loop (Properties[0].indexbuf_len) {
        if (IndexBuffer[i].is_line) {
            IndexBuffer[i].z = VertexBuffer[IndexBuffer[i].ur].camera_space[2];
            i += 1;
            continue;
        }
        int avg_z = VertexBuffer[IndexBuffer[i].ul].camera_space[2]
                    + VertexBuffer[IndexBuffer[i].ur].camera_space[2]
                    + VertexBuffer[IndexBuffer[i].dl].camera_space[2]
                    + VertexBuffer[IndexBuffer[i].dr].camera_space[2];

        IndexBuffer[i].z = avg_z / 4;
        // int max_z = VertexBuffer[IndexBuffer[i].ul].camera_space[2];
        // int tmp_z = VertexBuffer[IndexBuffer[i].ur].camera_space[2];
        // if (tmp_z > max_z) max_z = tmp_z;
        // tmp_z = VertexBuffer[IndexBuffer[i].dl].camera_space[2];
        // if (tmp_z > max_z) max_z = tmp_z;
        // tmp_z = VertexBuffer[IndexBuffer[i].dr].camera_space[2];
        // if (tmp_z > max_z) max_z = tmp_z;
        // IndexBuffer[i].z = max_z;

        i += 1;
    }

    cdb_sort_desc(2, Properties[0].indexbuf_len, 0);

    i = 0;
    loop (Properties[0].indexbuf_len) {
        if (IndexBuffer[i].is_line) {
            int ul = IndexBuffer[i].ul;
            int ur = IndexBuffer[i].ur;
            if (VertexBuffer[ul].camera_space[2] <= 0 ||
                VertexBuffer[ur].camera_space[2] <= 0)
            {
                i += 1;
                continue;
            }
            int len_x = VertexBuffer[ur].screen_x - VertexBuffer[ul].screen_x;
            int len_y = VertexBuffer[ur].screen_y - VertexBuffer[ul].screen_y;
            cmd [150](48,i,0,len_x,len_y,1,255,VertexBuffer[ul].screen_x,VertexBuffer[ul].screen_y,100,0,0,33554432,0,0,200,30,30)("<LINE-11>");
            i += 1;
            continue;
        }

        int ul = IndexBuffer[i].ul;
        int ur = IndexBuffer[i].ur;
        int dl = IndexBuffer[i].dl;
        int dr = IndexBuffer[i].dr;
        
        if (VertexBuffer[ul].camera_space[2] <= 0 ||
            VertexBuffer[ur].camera_space[2] <= 0 ||
            VertexBuffer[dl].camera_space[2] <= 0 ||
            VertexBuffer[dr].camera_space[2] <= 0)
        {
            i += 1;
            continue;
        }

        if (!render_is_facing(i)) {
            i += 1;
            continue;
        }

        int ul_x = VertexBuffer[ul].screen_x;
        int ul_y = VertexBuffer[ul].screen_y;
        int ur_x = VertexBuffer[ur].screen_x;
        int ur_y = VertexBuffer[ur].screen_y;
        int dl_x = VertexBuffer[dl].screen_x;
        int dl_y = VertexBuffer[dl].screen_y;
        int dr_x = VertexBuffer[dr].screen_x;
        int dr_y = VertexBuffer[dr].screen_y;

        cmd [150]
            (67112960,
            i,0,1,1,1,255,
            ul_x,ul_y,
            100,0,0,33554432,0,0,100,100,100,0,
            ur_x,ur_y,dl_x,dl_y,dr_x,dr_y)
            ("thing.png");
        // cmd [150]
        //     (67112960,
        //     i,0,1,6,i+1,255,
        //     ul_x,ul_y,
        //     100,0,0,33554432,0,0,100,100,100,0,
        //     ur_x,ur_y,dl_x,dl_y,dr_x,dr_y)
        //     ("numbered.png");

        i += 1;
    }

    Properties[0].vertexbuf_len = 0;
    Properties[0].indexbuf_len = 0;

    // reset all data
    cmd [250](2,-2,0,0,0)("","","","");
}

inline void render_init_cube(int cube, int half_side_len) {
    // top:
    // 0 1
    // 2 3
    // bottom:
    // 4 5
    // 6 7
    Cubes[cube].v0[0] = -half_side_len;
    Cubes[cube].v0[1] =  half_side_len;
    Cubes[cube].v0[2] =  half_side_len;

    Cubes[cube].v1[0] =  half_side_len;
    Cubes[cube].v1[1] =  half_side_len;
    Cubes[cube].v1[2] =  half_side_len;
    
    Cubes[cube].v2[0] = -half_side_len;
    Cubes[cube].v2[1] =  half_side_len;
    Cubes[cube].v2[2] = -half_side_len;
    
    Cubes[cube].v3[0] =  half_side_len;
    Cubes[cube].v3[1] =  half_side_len;
    Cubes[cube].v3[2] = -half_side_len;
    
    Cubes[cube].v4[0] = -half_side_len;
    Cubes[cube].v4[1] = -half_side_len;
    Cubes[cube].v4[2] =  half_side_len;
    
    Cubes[cube].v5[0] =  half_side_len;
    Cubes[cube].v5[1] = -half_side_len;
    Cubes[cube].v5[2] =  half_side_len;
    
    Cubes[cube].v6[0] = -half_side_len;
    Cubes[cube].v6[1] = -half_side_len;
    Cubes[cube].v6[2] = -half_side_len;
    
    Cubes[cube].v7[0] =  half_side_len;
    Cubes[cube].v7[1] = -half_side_len;
    Cubes[cube].v7[2] = -half_side_len;
}

inline void render_buffer_cube(int cube, int[12] transform) {
    int vert = Properties[0].vertexbuf_len;
    int quad = Properties[0].indexbuf_len;

    int[3] buf;
    
    mat3x4_transform(buf, transform, Cubes[cube].v0);
    render_buffer_vert(vert + 0, buf);
    mat3x4_transform(buf, transform, Cubes[cube].v1);
    render_buffer_vert(vert + 1, buf);
    mat3x4_transform(buf, transform, Cubes[cube].v2);
    render_buffer_vert(vert + 2, buf);
    mat3x4_transform(buf, transform, Cubes[cube].v3);
    render_buffer_vert(vert + 3, buf);
    mat3x4_transform(buf, transform, Cubes[cube].v4);
    render_buffer_vert(vert + 4, buf);
    mat3x4_transform(buf, transform, Cubes[cube].v5);
    render_buffer_vert(vert + 5, buf);
    mat3x4_transform(buf, transform, Cubes[cube].v6);
    render_buffer_vert(vert + 6, buf);
    mat3x4_transform(buf, transform, Cubes[cube].v7);
    render_buffer_vert(vert + 7, buf);

    IndexBuffer[quad + 0].ul = vert + 0;
    IndexBuffer[quad + 0].ur = vert + 1;
    IndexBuffer[quad + 0].dl = vert + 2;
    IndexBuffer[quad + 0].dr = vert + 3;
    
    IndexBuffer[quad + 1].ul = vert + 6;
    IndexBuffer[quad + 1].ur = vert + 7;
    IndexBuffer[quad + 1].dl = vert + 4;
    IndexBuffer[quad + 1].dr = vert + 5;
    
    IndexBuffer[quad + 2].ul = vert + 1;
    IndexBuffer[quad + 2].ur = vert + 0;
    IndexBuffer[quad + 2].dl = vert + 5;
    IndexBuffer[quad + 2].dr = vert + 4;
    
    IndexBuffer[quad + 3].ul = vert + 2;
    IndexBuffer[quad + 3].ur = vert + 3;
    IndexBuffer[quad + 3].dl = vert + 6;
    IndexBuffer[quad + 3].dr = vert + 7;
    
    IndexBuffer[quad + 4].ul = vert + 0;
    IndexBuffer[quad + 4].ur = vert + 2;
    IndexBuffer[quad + 4].dl = vert + 4;
    IndexBuffer[quad + 4].dr = vert + 6;
    
    IndexBuffer[quad + 5].ul = vert + 3;
    IndexBuffer[quad + 5].ur = vert + 1;
    IndexBuffer[quad + 5].dl = vert + 7;
    IndexBuffer[quad + 5].dr = vert + 5;

    Properties[0].vertexbuf_len += 8;
    Properties[0].indexbuf_len += 6;
}

inline void render_buffer_line(int[3] p, int[3] q) {
    int vert = Properties[0].vertexbuf_len;
    int quad = Properties[0].indexbuf_len;

    render_buffer_vert(vert, p);
    render_buffer_vert(vert+1, q);

    IndexBuffer[quad].ul = vert;
    IndexBuffer[quad].ur = vert+1;
    IndexBuffer[quad].is_line = 1;

    Properties[0].vertexbuf_len += 2;
    Properties[0].indexbuf_len += 1;
}

inline int render_is_facing(int quad) {
    int[3] ul_pos;
    int[3] a;
    int[3] b;
    vec3_copy(ul_pos, VertexBuffer[IndexBuffer[quad].ul].world_space);
    vec3_sub(a, VertexBuffer[IndexBuffer[quad].ur].world_space, ul_pos);
    vec3_sub(b, VertexBuffer[IndexBuffer[quad].dl].world_space, ul_pos);
    
    // in a real 3d application this normalization would not be necessary,
    // but not doing so here risks integer overflow
    vec3_normalize(a);
    vec3_normalize(b);

    int[3] n;
    vec3_cross(n, a, b);

    int[3] cam_pos;
    cam_pos[0] = Properties[0].look_at[3];
    cam_pos[1] = Properties[0].look_at[7];
    cam_pos[2] = Properties[0].look_at[11];

    int[3] line_of_sight;
    vec3_sub(line_of_sight, ul_pos, Properties[0].cam_pos);
    if (vec3_dot(n, line_of_sight) > 0) return 0;
    return 1;
}

// based on https://stackoverflow.com/questions/21114796/3d-ray-quad-intersection-test-in-java
inline int render_intersect_quad(int[3] r, int quad, int[3] origin, int[3] dir) {
    if (IndexBuffer[quad].is_line) return 0;

    int[3] n;
    int[3] a;
    int[3] b;

    int[3] ul_pos;
    vec3_copy(ul_pos, VertexBuffer[IndexBuffer[quad].ul].world_space);
    vec3_sub(a, VertexBuffer[IndexBuffer[quad].ur].world_space, ul_pos);
    vec3_sub(b, VertexBuffer[IndexBuffer[quad].dl].world_space, ul_pos);
    
    {
        // temporarily normalize a and b to avoid integer overflow when computing normal
        int[3] norm_a;
        vec3_copy(norm_a, a);
        vec3_normalize(norm_a);
        
        int[3] norm_b;
        vec3_copy(norm_b, b);
        vec3_normalize(norm_b);

        vec3_cross(n, norm_a, norm_b);
    }
    
    int denom = -vec3_dot(n, dir);
    if (denom == 0) {
        return 0;
    }

    int t;
    {
        int[3] tmp;
        vec3_sub(tmp, origin, ul_pos);
        t = vec3_dot(tmp, n) * 1000 / denom;
    }

    vec3_copy(r, origin);
    vec3_add_scaled(r, dir, t);

    int[3] from_ul;
    vec3_sub(from_ul, r, ul_pos);
    
    int u = vec3_dot(from_ul, a);
    int v = vec3_dot(from_ul, b);

    if (u >= 0 && u < vec3_dot(a, a) &&
        v >= 0 && v < vec3_dot(b, b))
    {
        return t;
    }
    return 0;
}

inline int test_draw_line(int pic, int[3] a, int[3] b) {
    int[3] start;
    int[3] end;
    vec3_copy(start, a);
    vec3_copy(end, b);
    render_buffer_vert(100, start);
    render_buffer_vert(101, end);
    int len_x = VertexBuffer[101].screen_x - VertexBuffer[100].screen_x;
    int len_y = VertexBuffer[101].screen_y - VertexBuffer[100].screen_y;
    cmd [150](48,pic,0,len_x,len_y,1,255,VertexBuffer[100].screen_x,VertexBuffer[100].screen_y,100,0,0,33554432,0,0,200,30,30)("<LINE-11>");
}