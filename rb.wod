cdb RigidBodies {
    int inv_mass;
    int[3] position;
    int[4] orientation;
    int[3] velocity;
    int[3] rotation;
    int[9] inv_inertia_tensor;

    int[3] force_accum;
    int[3] torque_accum;

    int linear_damping;
    int angular_damping;

    int[3] acceleration;
    int[3] last_frame_acceleration;

    // derived quantities
    int[12] transform;
    int[9] inv_inertia_tensor_world;

    // dimensions of the rigid body, used for collisions:
    // unlike in cyclone, assume that the collision primitive used is
    // always perfectly aligned with the rigid body, so there is no
    // need to have a separate collision primitive structure
    int half_size;

    // sleepy time
    int motion;
    int is_awake;
    int can_sleep;
}

const int RB_SLEEP_EPS = 300;

inline void rb_init_cube(int rb, int mass, int side_len) {
    RigidBodies[rb].half_size = side_len / 2;
    RigidBodies[rb].inv_mass = 1000000 / mass;
    RigidBodies[rb].acceleration[1] = -9810;
    int moment = (side_len * side_len) / 1000 * mass / 6000;
    int[9] it;
    it[0] = moment;
    it[1] = 0;
    it[2] = 0;
    it[3] = 0;
    it[4] = moment;
    it[5] = 0;
    it[6] = 0;
    it[7] = 0;
    it[8] = moment;
    rb_set_inertia_tensor(rb, it);
    rb_wake(rb);
    RigidBodies[rb].can_sleep = 1;
}

inline void rb_set_inertia_tensor(int rb, int[9] inertia_tensor) {
    mat3x3_invert(RigidBodies[rb].inv_inertia_tensor, inertia_tensor);
}

inline void rb_add_force(int rb, int[3] force) {
    vec3_add_update(RigidBodies[rb].force_accum, force);
    rb_wake(rb);
}

inline void rb_add_torque(int rb, int[3] torque) {
    vec3_add_update(RigidBodies[rb].torque_accum, torque);
    rb_wake(rb);
}

inline void rb_add_force_at_body_pt(int rb, int[3] force, int[3] point) {
    int[3] pt;
    mat3x4_transform(pt, RigidBodies[rb].transform, point);
    rb_add_force_at_pt(rb, force, pt);
}

inline void rb_add_force_at_pt(int rb, int[3] force, int[3] point) {
    int[3] pt;
    vec3_sub(pt, point, RigidBodies[rb].position);

    rb_add_force(rb, force);

    int[3] torque;
    vec3_cross(torque, pt, force);
    rb_add_torque(rb, torque);

    rb_wake(rb);
}

inline void rb_wake(int rb) {
    RigidBodies[rb].is_awake = 1;
    RigidBodies[rb].motion = RB_SLEEP_EPS * 2;
}

inline void rb_sleep(int rb) {
    RigidBodies[rb].is_awake = 0;
    vec3_clear(RigidBodies[rb].velocity);
    vec3_clear(RigidBodies[rb].rotation);
}

inline void rb_integrate(int rb, int duration) {
    if (!RigidBodies[rb].is_awake) return;
    vec3_copy(RigidBodies[rb].last_frame_acceleration, RigidBodies[rb].acceleration);
    vec3_add_scaled(
        RigidBodies[rb].last_frame_acceleration,
        RigidBodies[rb].force_accum,
        RigidBodies[rb].inv_mass
    );

    int[3] angular_acceleration;
    mat3x3_transform(
        angular_acceleration,
        RigidBodies[rb].inv_inertia_tensor_world,
        RigidBodies[rb].torque_accum
    );

    vec3_add_scaled(RigidBodies[rb].velocity, RigidBodies[rb].last_frame_acceleration, duration);
    vec3_add_scaled(RigidBodies[rb].rotation, angular_acceleration, duration);
    
    // NOTE: currently using constant damping
    vec3_scale_update(RigidBodies[rb].velocity, 995);
    vec3_scale_update(RigidBodies[rb].rotation, 995);

    int[3] vel;
    int[3] rot;
    vec3_copy(vel, RigidBodies[rb].velocity);
    vec3_copy(rot, RigidBodies[rb].rotation);

    vec3_add_scaled(RigidBodies[rb].position, vel, duration);
    quat_add_scaled(RigidBodies[rb].orientation, rot, duration);
    
    rb_calculate_derived(rb);
    rb_clear_accum(rb);

    if (RigidBodies[rb].can_sleep) {
        int current_motion = vec3_dot_safe(vel, vel) + vec3_dot_safe(rot, rot);

        const int BIAS = 970;
        int motion = (BIAS*RigidBodies[rb].motion + (1000-BIAS)*current_motion) / 1000;

        if (motion < RB_SLEEP_EPS) rb_sleep(rb);
        else if (motion > 10 * RB_SLEEP_EPS) motion = 10 * RB_SLEEP_EPS;

        RigidBodies[rb].motion = motion;
    }
}

inline void rb_clear_accum(int rb) {
    vec3_clear(RigidBodies[rb].force_accum);
    vec3_clear(RigidBodies[rb].torque_accum);
}

inline void rb_calculate_derived(int rb) {
    quat_normalize(RigidBodies[rb].orientation);

    _calculate_transform(
        RigidBodies[rb].transform, 
        RigidBodies[rb].position, 
        RigidBodies[rb].orientation
    );
    _transform_inertia_tensor(
        RigidBodies[rb].inv_inertia_tensor_world, 
        RigidBodies[rb].inv_inertia_tensor,
        RigidBodies[rb].transform
    );
}

// internal functions
inline void _calculate_transform(int[12] transform, int[3] pos, int[4] orientation) {
    transform[0]  = (500000 - orientation[2]*orientation[2] - orientation[3]*orientation[3]) / 500;
    transform[1]  =          (orientation[1]*orientation[2] - orientation[0]*orientation[3]) / 500;
    transform[2]  =          (orientation[1]*orientation[3] + orientation[0]*orientation[2]) / 500;
    transform[3]  = pos[0];

    transform[4]  =          (orientation[1]*orientation[2] + orientation[0]*orientation[3]) / 500;
    transform[5]  = (500000 - orientation[1]*orientation[1] - orientation[3]*orientation[3]) / 500;
    transform[6]  =          (orientation[2]*orientation[3] - orientation[0]*orientation[1]) / 500;
    transform[7]  = pos[1];

    transform[8]  =          (orientation[1]*orientation[3] - orientation[0]*orientation[2]) / 500;
    transform[9]  =          (orientation[2]*orientation[3] + orientation[0]*orientation[1]) / 500;
    transform[10] = (500000 - orientation[1]*orientation[1] - orientation[2]*orientation[2]) / 500;
    transform[11] = pos[2];
}

inline void _transform_inertia_tensor(int[9] iit_world, int[9] iit_body, int[12] rotmat) {
    int t4  = (rotmat[0]  * iit_body[0] +
               rotmat[1]  * iit_body[3] +
               rotmat[2]  * iit_body[6]) / 1000;
    int t9  = (rotmat[0]  * iit_body[1] +
               rotmat[1]  * iit_body[4] +
               rotmat[2]  * iit_body[7]) / 1000;
    int t14 = (rotmat[0]  * iit_body[2] +
               rotmat[1]  * iit_body[5] +
               rotmat[2]  * iit_body[8]) / 1000;
    int t28 = (rotmat[4]  * iit_body[0] +
               rotmat[5]  * iit_body[3] +
               rotmat[6]  * iit_body[6]) / 1000;
    int t33 = (rotmat[4]  * iit_body[1] +
               rotmat[5]  * iit_body[4] +
               rotmat[6]  * iit_body[7]) / 1000;
    int t38 = (rotmat[4]  * iit_body[2] +
               rotmat[5]  * iit_body[5] +
               rotmat[6]  * iit_body[8]) / 1000;
    int t52 = (rotmat[8]  * iit_body[0] +
               rotmat[9]  * iit_body[3] +
               rotmat[10] * iit_body[6]) / 1000;
    int t57 = (rotmat[8]  * iit_body[1] +
               rotmat[9]  * iit_body[4] +
               rotmat[10] * iit_body[7]) / 1000;
    int t62 = (rotmat[8]  * iit_body[2] +
               rotmat[9]  * iit_body[5] +
               rotmat[10] * iit_body[8]) / 1000;

    iit_world[0] = (t4  * rotmat[0] +
                    t9  * rotmat[1] +
                    t14 * rotmat[2])  / 1000;
    iit_world[1] = (t4  * rotmat[4] +
                    t9  * rotmat[5] +
                    t14 * rotmat[6])  / 1000;
    iit_world[2] = (t4  * rotmat[8] +
                    t9  * rotmat[9] +
                    t14 * rotmat[10]) / 1000;
    iit_world[3] = (t28 * rotmat[0] +
                    t33 * rotmat[1] +
                    t38 * rotmat[2])  / 1000;
    iit_world[4] = (t28 * rotmat[4] +
                    t33 * rotmat[5] +
                    t38 * rotmat[6])  / 1000;
    iit_world[5] = (t28 * rotmat[8] +
                    t33 * rotmat[9] +
                    t38 * rotmat[10]) / 1000;
    iit_world[6] = (t52 * rotmat[0] +
                    t57 * rotmat[1] +
                    t62 * rotmat[2])  / 1000;
    iit_world[7] = (t52 * rotmat[4] +
                    t57 * rotmat[5] +
                    t62 * rotmat[6])  / 1000;
    iit_world[8] = (t52 * rotmat[8] +
                    t57 * rotmat[9] +
                    t62 * rotmat[10]) / 1000;
}
